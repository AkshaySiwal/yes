// Jenkinsfile
def getReactiveParams() {
    return [
        choice(
            name: 'CONFIGURATION_METHOD',
            choices: ['', 'Upload CSV File', 'Manual Entry'],
            description: 'Select how you want to provide Account and Role information'
        ),
        choice(
            name: 'TASK',
            choices: ['Backup', 'Backup and Delete'],
            description: 'Select the task to perform'
        ),
        [$class: 'DynamicReferenceParameter',
            choiceType: 'ET_FORMATTED_HTML',
            name: 'Account',
            referencedParameters: 'CONFIGURATION_METHOD',
            script: [
                $class: 'GroovyScript',
                fallbackScript: [classpath: [], sandbox: true, script: 'return ""'],
                script: [
                    classpath: [], 
                    sandbox: true,
                    script: '''
                        if (CONFIGURATION_METHOD == 'Manual Entry') {
                            return """
                                <div class='form-group'>
                                    <input type='text' 
                                           class='setting-input' 
                                           name='value'
                                           placeholder='Enter Account ID'
                                           style='width: 300px; padding: 5px;'
                                           required>
                                </div>
                            """
                        }
                        return "NA"
                    '''
                ]
            ]
        ],
        [$class: 'DynamicReferenceParameter',
            choiceType: 'ET_FORMATTED_HTML',
            name: 'Role',
            referencedParameters: 'CONFIGURATION_METHOD',
            script: [
                $class: 'GroovyScript',
                fallbackScript: [classpath: [], sandbox: true, script: 'return ""'],
                script: [
                    classpath: [], 
                    sandbox: true,
                    script: '''
                        if (CONFIGURATION_METHOD == 'Manual Entry') {
                            return """
                                <div class='form-group'>
                                    <input type='text' 
                                           class='setting-input' 
                                           name='value' 
                                           placeholder='Enter Role Name'
                                           style='width: 300px; padding: 5px;'
                                           required>
                                </div>                                
                            """
                        }
                        return "NA"
                    '''
                ]
            ]
        ],
        [$class: 'DynamicReferenceParameter',
            choiceType: 'ET_FORMATTED_HTML',
            name: 'File',
            referencedParameters: 'CONFIGURATION_METHOD',
            script: [
                $class: 'GroovyScript',
                fallbackScript: [classpath: [], sandbox: true, script: 'return ""'],
                script: [
                    classpath: [], 
                    sandbox: true,
                    script: '''
                        if (CONFIGURATION_METHOD == 'Upload CSV File') {
                            return """
                                <div class='form-group'>
                                    <label class='control-label' style='display: block; font-weight: bold; margin-bottom: 5px;'>
                                        Upload CSV File <span style='color: #FF0000;'>*</span>
                                    </label>
                                    <div style='font-size: 12px; color: #666; margin-top: 5px;'>
                                        Please upload a CSV file with the following format:
                                        <ul style='margin-top: 5px; margin-bottom: 10px;'>
                                            <li>First column: AWS Account ID (12 digits)</li>
                                            <li>Second column: IAM Role name</li>
                                        </ul>
                                        Example:
                                        <pre style='background: #f5f5f5; padding: 8px; margin-top: 5px; border-radius: 4px;'>
123456789012,role1
123456789012,role2
987654321098,role1</pre>
                                    </div>
                                    <input type='file' 
                                           class='setting-input' 
                                           name='value'
                                           accept='.csv'
                                           style='width: 100%; max-width: 400px; padding: 8px; border: 1px solid #ccc; border-radius: 4px;'
                                           required>
                                </div>
                            """
                        }
                        return "NA"
                    '''
                ]
            ]
        ]
    ]
}

properties([
    parameters(getReactiveParams())
])

pipeline {
    agent any
  
    options {
        disableConcurrentBuilds()
    }
  
    stages {
        stage('Parameter Validation') {
            steps {
                script {
                    if (!params.CONFIGURATION_METHOD) {
                        currentBuild.result = 'SUCCESS'
                        return
                    }

                    // Map for task values
                    def taskMap = [
                        'Backup': 'backup',
                        'Backup and Delete': 'delete'
                    ]
                    
                    // Get the task value from the map
                    def taskValue = taskMap[params.TASK]
                    if (!taskValue) {
                        error "Invalid task selected"
                    }

                    if (params.CONFIGURATION_METHOD == 'Manual Entry') {
                        if (!params.Account?.trim() || !params.Role?.trim()) {
                            error "Account and Role are required for Manual selection"
                        }
                        echo "Running in Manual mode with Account: ${params.Account} and Role: ${params.Role}"
                        echo "Selected task: ${params.TASK} (${taskValue})"
                    } else if (params.CONFIGURATION_METHOD == 'Upload CSV File') {
                        if (!params.File) {
                            error "File is required for File selection"
                        }
                        echo "Running in File mode with uploaded file"
                        echo "Selected task: ${params.TASK} (${taskValue})"
                    }
                }
            }
        }
      
        stage('Run Python Script') {
            when {
                expression { params.CONFIGURATION_METHOD != '' }
            }
            steps {
                script {
                    def taskMap = [
                        'Backup': 'backup',
                        'Backup and Delete': 'delete'
                    ]
                    
                    withEnv([
                        "CONFIGURATION_METHOD=${params.CONFIGURATION_METHOD}",
                        "Account=${params.Account ?: ''}",
                        "Role=${params.Role ?: ''}",
                        "File=${params.File ?: ''}",
                        "TASK=${taskMap[params.TASK]}"
                    ]) {
                        sh 'python3 script.py'
                    }
                }
            }
        }
    }
}
